import groovy.json.JsonSlurper

allprojects {

    repositories {
        jcenter()
        mavenCentral()
    }

    project.ext.set("publishGroupId", group)
}

tasks.create('createTestSubAccount') {
    doFirst {
        println("Task createTestSubAccount called with module $moduleName")

        // core does not use test clouds, skip
        if (moduleName == "core") {
            println "Skipping test cloud creation..."
            return
        }

        println "Creating test cloud..."
        def baseUrl = new URL('https://sub-account-testing.cloudinary.com/create_sub_account')
        def connection = baseUrl.openConnection()
        connection.with {
            doOutput = true
            requestMethod = 'POST'
            def json = new JsonSlurper().parseText(content.text)
            def cloud = json["payload"]["cloudName"]
            def key = json["payload"]["cloudApiKey"]
            def secret = json["payload"]["cloudApiSecret"]
            def cloudinaryUrl = "CLOUDINARY_URL=cloudinary://$key:$secret@$cloud"

            def dir = new File("${projectDir.path}${File.separator}tools")
            dir.mkdir()
            def file = new File(dir , "cloudinary_url.txt")
            file.createNewFile()
            file.text = cloudinaryUrl

            println("Test sub-account created succesfully!")
        }
    }
}
